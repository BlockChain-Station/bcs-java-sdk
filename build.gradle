buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        // This is a custom version of the plugin. To build clone github.com/favorlock/gradle-plugin
        // and run the following gradle command: clean publishToMavenLocal -Pversion=3.3.0
        classpath group: 'io.freefair.lombok',
                  name: 'io.freefair.lombok.gradle.plugin',
                  version: '3.3.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'java'
    id 'java-library'
    id 'maven'
//    id 'io.freefair.lombok' version '3.2.0'
}

apply plugin: 'io.freefair.lombok'

apply from: 'gradle/util.gradle'

apply from: scriptFile('env.gradle')
configureJavaCompilers(8)

group = 'com.enjin'
version = '0.6.0-SNAPSHOT'

ext {
    enjinCommons = '0.3.0-SNAPSHOT'
    mockito = '2.9.0'
    junit = '4.12'
    powermock = '1.7.4'
    assertj = '3.11.1'
    gson = '2.8.5'
    gsonJava8Datatype = '1.1.0'
    pusher = '1.8.0'
    okhttp = '3.12.1'
    retrofit = '2.5.0'
    lombok = '1.18.6'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // implementation dependencies
    implementation group: 'com.enjin', name: 'java_commons', version: enjinCommons
    implementation group: 'com.google.code.gson', name: 'gson', version: gson
    implementation group: 'net.dongliu', name: 'gson-java8-datatype', version: gsonJava8Datatype
    implementation group: 'com.pusher', name: 'pusher-java-client', version: pusher
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: okhttp
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: retrofit
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: retrofit

    // testImplementation dependencies
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockito
    testImplementation group: 'junit', name: 'junit', version: junit
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powermock
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: powermock
    testImplementation group: 'org.assertj', name: 'assertj-core', version: assertj
}

javadoc {
    dependsOn delombok
    source = delombok.target
    // exclude any implementation packages
    exclude '**/impl/**'
    // exclude any implementation classes
    exclude '**/*Impl.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives javadocJar
    archives sourcesJar
    archives shadowJar
}

wrapper {
    gradleVersion = '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}