buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

apply from: 'gradle/util.gradle'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: scriptFile('env.gradle')
configureJavaCompilers(8)

group = 'com.enjin.enjincoin'
version = '0.3.1-SNAPSHOT'

idea {
    module {
        scopes.PROVIDED.plus += [configurations.shadow]
    }
}

tasks.build.dependsOn(shadowJar)
artifacts.archives shadowJar

tasks.withType(AbstractCompile) {
    classpath += configurations.shadow
}

ext {
    enjinCommons = '0.3.0-SNAPSHOT'
    mockito = '2.9.0'
    junit = '4.12'
    powermock = '1.7.4'
    assertj = '3.11.1'
    gson = '2.8.5'
    gsonJava8Datatype = '1.1.0'
    pusher = '1.8.0'
    okhttp = '3.12.1'
    retrofit = '2.5.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // implementation dependencies
    implementation group: 'com.enjin', name: 'java_commons', version: enjinCommons
    implementation group: 'com.google.code.gson', name: 'gson', version: gson
    implementation group: 'net.dongliu', name: 'gson-java8-datatype', version: gsonJava8Datatype
    implementation group: 'com.pusher', name: 'pusher-java-client', version: pusher
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: okhttp
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: retrofit
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: retrofit

    // testImplementation dependencies
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockito
    testImplementation group: 'junit', name: 'junit', version: junit
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powermock
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: powermock
    testImplementation group: 'org.assertj', name: 'assertj-core', version: assertj
}

jar {
    archiveName = "${project.name}-slim.jar"
}

shadowJar {
    archiveName = "${project.name}.jar"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.artifactId = "java-sdk-${project.name}"
    }
}