buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'java'
    id 'java-library'
    id 'maven'
    id 'io.freefair.lombok' version '3.2.1'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.20.0'
    id 'org.ajoberstar.git-publish' version '2.1.1'
}

apply plugin: 'io.freefair.lombok'

apply from: 'gradle/util.gradle'

apply from: scriptFile('env.gradle')
configureJavaCompilers(8)

group = 'com.enjin'
version = '0.7.0-SNAPSHOT'

ext {
    enjinCommons = '0.3.0-SNAPSHOT'
    mockito = '2.9.0'
    junit = '4.12'
    powermock = '1.7.4'
    assertj = '3.11.1'
    gson = '2.8.5'
    gsonJava8Datatype = '1.1.0'
    pusher = '1.8.0'
    okhttp = '3.12.1'
    retrofit = '2.5.0'
    lombok = '1.18.6'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // implementation dependencies
    implementation group: 'com.enjin', name: 'java_commons', version: enjinCommons
    implementation group: 'com.google.code.gson', name: 'gson', version: gson
    implementation group: 'net.dongliu', name: 'gson-java8-datatype', version: gsonJava8Datatype
    implementation group: 'com.pusher', name: 'pusher-java-client', version: pusher
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: okhttp
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: retrofit
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: retrofit

    // testImplementation dependencies
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockito
    testImplementation group: 'junit', name: 'junit', version: junit
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powermock
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: powermock
    testImplementation group: 'org.assertj', name: 'assertj-core', version: assertj
}

javadoc {
    dependsOn delombok
    source = delombok.target
    // exclude any implementation packages
    exclude '**/impl/**'
    // exclude any implementation classes
    exclude '**/*Impl.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives javadocJar
    archives sourcesJar
    archives shadowJar
}

modifyPom {
    project {
        name 'Enjin Coin Java SDK'
        description 'Java SDK for interacting with the Enjin Coin Trusted Platform'
        url 'https://github.com/enjin/Enjin-Coin-Java-SDK'
        inceptionYear '2017'

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'Favorlock'
                name 'Evan Lindsay'
                email 'evan@enjin.io'
            }
        }
    }
}

extraArchive {
    sources = false
    javadoc = false
    tests = false
}

nexus {
    sign = false
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

gitPublish {
    repoUri = 'git@github.com:enjin/Enjin-Coin-Java-SDK.git'
    branch = 'gh-pages'
    contents {
        from(javadoc) {
            into '/'
        }
    }
    commitMessage = "Publishing documentation for version ${version}."
}

wrapper {
    gradleVersion = '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}